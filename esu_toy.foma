#!/usr/bin/env foma -f

read lexc esu_toy.lexc
define Lexicon

define WordBoundary [ .#. ];

define Stop        [ p | t | c | k | q ];
define Nasal       [ m | n | "ng" | 
                     ḿ | ń | "ńg" ];
define Fricative   [  v   |  l   |  s   |  g   |  r   |
                     "vv" | "ll" | "ss" | "gg" | "rr" |
                     "u͡g" | "u͡gg" | "u͡r" | "u͡rr" ];
define C           [ Stop | Nasal | Fricative | w | y ];

define FullVowel   [ a | i | u ];
define V           [ e | FullVowel ];

define Alphabet [ C | V ];

define MorphPhonSymbols     [ "~" | "+" | "-" | ":" | "@" | "`" |
                              "(ng)" | "(s)" | "(g)" | "(t)" ];


#######################################################

define Allomorphy           "(ng)" -> "ng",
                            "(s)" -> s      || V MorphPhonSymbols* _         .o.
                            "(t)" -> t      || [ g | r ] MorphPhonSymbols* _ .o.
                            "(g)" -> g      || V V MorphPhonSymbols* _       .o.
                            [ "(ng)" | "(s)" | "(g)" | "(t)" ] -> 0 ;


define VelarDroping         [ g | r | "ng" ] -> 0 || C V _ ":" V C          .o.
                            ":" -> 0 ;

define KeepConsonant        "+" -> 0 ;

define DropConsonant        C -> 0 || _ MorphPhonSymbols* "-" .o.
                            "-" -> 0 ;

define EDeletion            e -> 0 || _ MorphPhonSymbols* "~" .o.
                            "~" -> 0 ;

define BaseFinalEndings     r -> q,
                            g -> k,
                            e -> a || _ ([= Alphabet*]*) WordBoundary ;

define RemoveMorphon        [ "@" | "`" ] -> 0 ;

define TripleConsonant      [..] -> e || C C _ C ;

define Engi                 e ng i -> a i ;

#######################################################

define Grammar [ 
            Allomorphy          .o.
            DropConsonant       .o.
            KeepConsonant       .o.
            EDeletion           .o.
            VelarDroping        .o.
            RemoveMorphon       .o.
            BaseFinalEndings    .o.
            TripleConsonant     .o.
            Engi ] ;

#######################################################

regex Lexicon .o. Grammar ;
save stack esuMorph.fst
